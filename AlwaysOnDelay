IF (SELECT SERVERPROPERTY('IsHadrEnabled')) = 1
BEGIN
WITH AG_Stats AS (
        SELECT AGS.name AS AGGroupName, 
                AR.replica_server_name AS InstanceName, 
                HARS.role_desc, 
                Db_name(DRS.database_id) AS DBName, 
                DRS.database_id, 
                AR.availability_mode_desc AS SyncMode, 
                DRS.synchronization_state_desc AS SyncState, 
                DRS.last_hardened_lsn, 
                DRS.end_of_log_lsn, 
                DRS.last_redone_lsn, 
                DRS.last_hardened_time, -- On a secondary database, time of the log-block identifier for the last hardened LSN (last_hardened_lsn).
                DRS.last_redone_time, -- Time when the last log record was redone on the secondary database.
                DRS.log_send_queue_size, 
				DRS.redo_queue_size,
                DRS.last_commit_time
        FROM   sys.dm_hadr_database_replica_states DRS 
        LEFT JOIN sys.availability_replicas AR 
        ON DRS.replica_id = AR.replica_id 
        LEFT JOIN sys.availability_groups AGS 
        ON AR.group_id = AGS.group_id 
        LEFT JOIN sys.dm_hadr_availability_replica_states HARS ON AR.group_id = HARS.group_id 
        AND AR.replica_id = HARS.replica_id ),
    Pri_CommitTime AS 
        ( SELECT  AGGroupName,DBName, last_commit_time
        FROM    AG_Stats
        WHERE   role_desc = 'PRIMARY' ),
    Rpt_CommitTime AS 
        ( SELECT  AGGroupName,DBName, last_commit_time
        FROM    AG_Stats
        WHERE   role_desc = 'SECONDARY' AND [InstanceName] in (select distinct replica_server_name from [master].sys.availability_replicas) ),
	CTE_TDelay AS 
		( SELECT instance_name, cntr_value FROM sys.dm_os_performance_counters 
		WHERE RTRIM(object_name) like '%:Database Replica' AND counter_name = 'Transaction Delay' AND RTRIM(instance_name) <> '_Total' ),
	CTE_WritePS AS  
		( SELECT instance_name, cntr_value FROM sys.dm_os_performance_counters 
		WHERE RTRIM(object_name) like '%:Database Replica' AND counter_name = 'Mirrored Write Transactions/sec' AND RTRIM(instance_name) <> '_Total' )

SELECT DISTINCT 
  @@SERVERNAME AS InstanceName
, p.[AGGroupName]
, p.[DBName] AS [DatabaseName]
, dns_name as ListenerName
, AGI.ip_address as ListenerIP
, port as ListenerPort
, role_desc as 'Role'
, availability_mode_desc as 'AvailabilityMode'
, failover_mode_desc as 'FailoverMode'
, UPPER(automated_backup_preference_desc) as 'BackupPreference'
, DRS.synchronization_health_desc as 'SynchronizationHealth'
, CASE 
WHEN availability_mode_desc = 'ASYNCHRONOUS_COMMIT' THEN NULL   -- OR 'ASYNCHRONOUS'
WHEN availability_mode_desc = 'SYNCHRONOUS_COMMIT' THEN (CTE_TDelay.cntr_value)/(CTE_WritePS.cntr_value) 
END AS [PrimaryCommitDelay(ms)]
, DATEDIFF(ss,r.last_commit_time,p.last_commit_time) AS [SecondarySyncDelay(sec)]
--, p.last_commit_time AS [PrimaryLastCommitTime]
--, r.last_commit_time AS [SecondaryLastCommitTime]
--, getdate() AS [TimeStamp]
FROM Pri_CommitTime p
LEFT JOIN Rpt_CommitTime r ON [r].[DBName] = [p].[DBName]
LEFT JOIN CTE_TDelay ON CTE_TDelay.instance_name = [p].[DBName]
LEFT JOIN CTE_WritePS ON CTE_TDelay.instance_name = CTE_WritePS.instance_name AND CTE_WritePS.cntr_value > 0
LEFT JOIN sys.availability_groups_cluster AGC ON AGC.name = p.[AGGroupName]
LEFT JOIN [master].sys.dm_hadr_availability_replica_cluster_states AS RCS ON RCS.group_id = AGC.group_id
LEFT JOIN [master].sys.dm_hadr_availability_replica_states AS ARS ON ARS.replica_id = RCS.replica_id
LEFT JOIN [master].sys.availability_group_listeners AS AGL ON AGL.group_id = ARS.group_id
LEFT JOIN [master].sys.availability_group_listener_ip_addresses AS AGI ON AGI.listener_id = AGL.listener_id
LEFT JOIN [master].sys.availability_databases_cluster AS AGD ON AGD.group_id = AGL.group_id
LEFT JOIN [master].sys.availability_replicas AS ARN ON ARS.replica_id = ARN.replica_id AND ars.is_local = 1
LEFT JOIN [master].sys.dm_hadr_database_replica_states DRS ON DRS.group_id = ARS.group_id
WHERE role_desc = 'PRIMARY'
ORDER BY 1, 2, 3
END
