CREATE TABLE #PublicRoleResults(PermissionType nvarchar(50), PermissionName nvarchar(200), DatabaseRole nvarchar(200), DatabaseName nvarchar(200), ObjectSchema nvarchar(200), ObjectName nvarchar(200), ObjectType nvarchar(200), GrantPermissionTSQL nvarchar(200), RevokePermissionTSQL nvarchar(200))
INSERT INTO #PublicRoleResults
EXEC sp_msforeachdb 'USE [?] 
IF ''?'' NOT IN (''master'',''model'',''msdb'' ,''tempdb'')
BEGIN
SELECT p.[state_desc] AS [PermissionType]
        ,p.[permission_name] AS [PermissionName]
        ,USER_NAME(p.[grantee_principal_id]) AS [DatabaseRole]
        ,CASE p.[class]
            WHEN 0
                THEN ''Database::'' + DB_NAME()
            WHEN 1
                THEN OBJECT_NAME(major_id)
            WHEN 3
                THEN ''Schema::'' + SCHEMA_NAME(p.[major_id])
            END AS [ObjectName]
INTO #PublicRoleDBPermissions
FROM [sys].[database_permissions] p
WHERE p.[class] IN (0, 1, 3)
AND p.[minor_id] = 0

SELECT [PermissionType]
    ,[PermissionName] COLLATE DATABASE_DEFAULT AS PermissionName
    ,[DatabaseRole] COLLATE DATABASE_DEFAULT AS DatabaseRole
    ,DB_NAME() as DatabaseName
	,SCHEMA_NAME(o.[schema_id]) AS [ObjectSchema]
    ,[ObjectName] COLLATE DATABASE_DEFAULT AS ObjectName
    ,o.[type_desc] AS [ObjectType]
    ,[PermissionType] + '' '' + [PermissionName] COLLATE DATABASE_DEFAULT + '' ON '' + QUOTENAME(SCHEMA_NAME(o.[schema_id])) + ''.'' + QUOTENAME([ObjectName] COLLATE DATABASE_DEFAULT) + '' TO '' + QUOTENAME([DatabaseRole] COLLATE DATABASE_DEFAULT) AS [GrantPermissionTSQL]
    ,''REVOKE'' + '' '' + [PermissionName] COLLATE DATABASE_DEFAULT + '' ON '' + QUOTENAME(SCHEMA_NAME(o.[schema_id])) + ''.'' + QUOTENAME([ObjectName] COLLATE DATABASE_DEFAULT) + '' TO '' + QUOTENAME([DatabaseRole] COLLATE DATABASE_DEFAULT) AS [RevokePermissionTSQL]
FROM #PublicRoleDBPermissions p
INNER JOIN [sys].[objects] o
    ON o.[name] = p.[ObjectName]
        AND OBJECTPROPERTY(o.object_id, ''IsMSShipped'') = 0
WHERE [DatabaseRole] = ''Public'' and [ObjectName] not in (''fn_diagramobjects'',''sp_alterdiagram'',''sp_creatediagram'',''sp_dropdiagram'',''sp_helpdiagramdefinition'',''sp_helpdiagrams'',''sp_renamediagram'')
ORDER BY [DatabaseRole]
    ,[ObjectName]
    ,[ObjectType]

DROP TABLE #PublicRoleDBPermissions
END
'
SELECT * FROM #PublicRoleResults
DROP TABLE #PublicRoleResults
